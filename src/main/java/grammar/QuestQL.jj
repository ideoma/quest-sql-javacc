/**
 *
 *
 */
/*

Options

*/
options{
    IGNORE_CASE=true ;
    MULTI=true;
    NODE_DEFAULT_VOID=true;
    JAVA_TEMPLATE_TYPE="modern" ;
//  DEBUG_LOOKAHEAD= true ;
}

/*

Entry point

*/
PARSER_BEGIN(Parser)
package parse;

import io.questdb.griffin.model.ExpressionNode;
import io.questdb.std.ObjectPool;
import java.util.ArrayDeque;
import java.util.Deque;

public class Parser
{
    private ObjectPool<ExpressionNode> sqlNodePool = new ObjectPool<ExpressionNode>(ExpressionNode.FACTORY, 100);
    private final Deque<ExpressionNode> stack = new ArrayDeque<>();
    public ExpressionNode selectNode, fromNode, whereNode;

    public Parser() {
    }

    public void parse(String args) throws Exception {
        stack.clear();
        selectNode = fromNode = whereNode = null;
        ReInit(args);
        SQLStatement();
        stack.clear();
    }

    private void popSelect(String modifier) {
        pop(ExpressionNode.QUERY, "SELECT", 0);
        selectNode = stack.pop();
    }

    private void popFrom() {
        pop(ExpressionNode.QUERY, "FROM", 0);
        fromNode = stack.pop();
    }

    private void popWhere() {
        pop(ExpressionNode.QUERY, "WHERE", 0);
        whereNode = stack.pop();
    }

    private void push(int type, Token token) {
        ExpressionNode newNode = sqlNodePool.next().of(type, token.image, Integer.MIN_VALUE, token.beginColumn);
        stack.push(newNode);
    }

    private void push(int type, CharSequence token, int position) {
        ExpressionNode newNode = sqlNodePool.next().of(type, token, Integer.MIN_VALUE, position);
        stack.push(newNode);
    }

    private void pop(int type, CharSequence token, int position) {
        pop(type, token, position, stack.size());
    }

    private void pop(int type, CharSequence token, int position, int parameters) {
        ExpressionNode newNode = sqlNodePool.next().of(type, token, Integer.MIN_VALUE, position);
        assert stack.size() >= parameters;
        switch(parameters) {
            case 0:
                break;
            case 1:
                newNode.rhs = stack.pop();
                break;
            case 2:
                newNode.rhs = stack.pop();
                newNode.lhs = stack.pop();
                break;
            default:
               for (int i = 0; i < parameters; i++) {
                   newNode.args.add(stack.pop());
               }
               break;
        }
        stack.push(newNode);
    }

    private void replace(int type, CharSequence token, int position, int parameters) {
        ExpressionNode newNode = sqlNodePool.next().of(type, token, Integer.MIN_VALUE, position);
        assert stack.size() >= parameters;
        for(int i = 0; i < parameters; i++) {
            stack.pop();
        }
        stack.push(newNode);
    }
}

PARSER_END(Parser)

/*

set up tokens first. SKIP tokens of " ", "\t", "\r" and "\n"

*/
SKIP:
{
    " "
|   "\t"
|   "\r"
|   "\n"
}

/*

Quest SQL Keywords

*/
TOKEN:
{
    <K_ALL: "ALL">
|   <K_ALTER: "ALTER">
|   <K_AND: "AND">
|   <K_ANY: "ANY">
|   <K_AS: "AS">
|   <K_ASC:"ASC">
|   <K_BEGIN: "BEGIN">
|   <K_BETWEEN:"BETWEEN">
|   <K_BOOLEAN:"BOOLEAN">
|   <K_BY:"BY">
|   <K_CACHE:"CACHE">
|   <K_CASE:"CASE">
|   <K_CAST:"CAST">
|   <K_CHAR:"CHAR">
|   <K_CAPACITY:"CAPACITY">
|   <K_DATE:"DATE">
|   <K_DESC:"DESC">
|   <K_DISTINCT:"DISTINCT">
|   <K_DOUBLE:"DOUBLE">
|   <K_ELSE:"ELSE">
|   <K_END:"END">
|   <K_FLOAT:"FLOAT">
|   <K_FROM:"FROM">
|   <K_FULL:"FULL">
|   <K_IN:"IN">
|   <K_INDEX:"INDEX">
|   <K_INNER:"INNER">
|   <K_INTEGER:"INTEGER">
|   <K_INTO:"INTO">
|   <K_IS:"IS">
|   <K_LEFT:"LEFT">
|   <K_LIKE:"LIKE">
|   <K_NOCACHE: "NOCACHE">
|   <K_NOT:"NOT">
|   <K_NULL:"NULL">
|   <K_OR:"OR">
|   <K_ORDER:"ORDER">
|   <K_PARTITION:"PARTITION">
|   <K_SAMPLE:"SAMPLE">
|   <K_SELECT:"SELECT">
|   <K_SET:"SET">
|   <K_TABLE:"TABLE">
|   <K_THEN:"THEN">
|   <K_UNION:"UNION">
|   <K_VALUES:"VALUES">
|   <K_STRING:"STRING">
|   <K_WHERE:"WHERE">
|   <K_WHEN:"WHEN">
|   <K_WITH:"WITH">
}

TOKEN : /* Numeric Constants */
{
	< S_NUMBER: <FLOAT>
	    | <FLOAT> ( ["e","E"] ([ "-","+"])? <FLOAT> )?
    	>
  | 	< #FLOAT: <INTEGER>
	    | <INTEGER> ( "." <INTEGER> )?
	    | "." <INTEGER>
    	>
  | 	< #INTEGER: ( <DIGIT> )+ >
  | 	< #DIGIT: ["0" - "9"] >
}

SPECIAL_TOKEN:
{
   <LINE_COMMENT: "--"(~["\r","\n"])*>
|  <MULTI_LINE_COMMENT: "/*" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
}

TOKEN:
{
    < S_IDENTIFIER: (<LETTER>)+ (<DIGIT> | <LETTER> |<SPECIAL_CHARS>)* >
  | < #LETTER: ["a"-"z", "A"-"Z"] >
  | < #SPECIAL_CHARS: "$" | "_" | "#">
  | < S_BIND: ":" ( <S_NUMBER> | <S_IDENTIFIER> ("." <S_IDENTIFIER>)?) >
  | < S_CHAR_LITERAL: "'" (~["'"])* "'" ("'" (~["'"])* "'")*>
  | < S_QUOTED_IDENTIFIER: "\"" (~["\n","\r","\""])* "\"" >
}

void BindVariable():
{}
{
    <S_BIND>
  | "$" (<S_NUMBER> | <S_IDENTIFIER>)
}

void BasicDataTypeDefinition():
{}
{
    ( "BOOLEAN"
    |   "BYTE"
    |   "SHORT"
    |   "CHAR"
    |   "INT"
    |   "FLOAT"
    |   "SYMBOL" //[<K_CAPACITY> <S_NUMBER> [<K_CACHE> | <K_NOCACHE>] [<K_INDEX> ]]
    |   "STRING"
    |   "LONG"
    |   "DATE"
    |   "TIMESTAMP"
    |   "DOUBLE"
    |   "BINARY"
    |   "LONG256"
    )
    {push(ExpressionNode.CONSTANT, token);}
}

void SQLStatement():
{}
{
    InsertStatement()
    |
    QueryStatement()
}

void InsertStatement():
{}
{
    "INSERT" "INTO" TableName() {}
     [ LOOKAHEAD(2) "(" TableColumn() ("," TableColumn())* ")" ]
    ( "VALUES" "(" SqlExpressionList() ")"
      | SubQuery()
    )
    ";"
}

void QueryStatement():
{}
{
    SelectStatement()
    (";" | <EOF>)
}

void SqlExpression():
{}
{
    SqlAndExpression() ("OR" SqlAndExpression() {
        pop(ExpressionNode.FUNCTION, "OR", token.beginColumn, 2);
    })*
}

void SqlAndExpression():
{}
{
    SqlUnaryLogicalExpression() ( "AND" SqlUnaryLogicalExpression() {
        pop(ExpressionNode.FUNCTION, "AND", token.beginColumn, 2);
    })*
}

void SqlUnaryLogicalExpression():
{ boolean inverted = false; }
{
  ["NOT" {inverted = true;}] SqlRelationalExpression() {
    if (inverted) {
        pop(ExpressionNode.FUNCTION, "NOT", token.beginColumn, 1);
    }
  }
}

void SqlRelationalExpression():
{
    String operation;
}
{
    SqlSimpleExpression()

    ( Relop() { operation = token.image; } SqlSimpleExpression() { pop(ExpressionNode.OPERATION, operation, token.beginColumn, 2); }
      |
      LOOKAHEAD(2) SqlInClause()
      |
      LOOKAHEAD(2) SqlBetweenClause()
      |
      LOOKAHEAD(2) SqlLikeClause()
      |
      IsNullClause()
   )?
}

void SqlExpressionList():
{}
{
    SqlExpression() ("," SqlExpression())*
}

void SqlInClause():
{}
{
    ["NOT"] "IN" "(" SqlExpressionList()")"
}

void SqlBetweenClause():
{}
{
    ["NOT"] "BETWEEN" SqlSimpleExpression() "AND" SqlSimpleExpression()
}

void SqlLikeClause():
{}
{
    ["NOT"] "LIKE" SqlSimpleExpression()
}

void IsNullClause():
{}
{
    "IS" ["NOT"] "NULL"
}

void SqlSimpleExpression():
{}
{
    SqlMultiplicativeExpression() ( ("+" | "-" | "||")
                                 SqlMultiplicativeExpression() { pop(ExpressionNode.FUNCTION, token.image, token.beginColumn, 2); }
                               )*
}

void SqlMultiplicativeExpression():
{}
{
    SqlUnaryExpression() ( LOOKAHEAD(1) ("*" | "/") SqlUnaryExpression() { pop(ExpressionNode.FUNCTION, token.image, token.beginColumn, 2); })*
}

void SqlUnaryExpression():
{boolean hasMinus = false;}
{
    (("+" | "-" {hasMinus = true;}) SqlPrimaryExpression(hasMinus))
    |
    SqlPrimaryExpression(false)
}

void SqlPrimaryExpression(boolean hasMinus):
{}
{
    <S_NUMBER> { push(ExpressionNode.LITERAL, token);}
  | <S_CHAR_LITERAL> { push(ExpressionNode.LITERAL, token);}
  | "NULL" { push(ExpressionNode.LITERAL, token);}
  | SqlCaseExpression()
  | "(" (LOOKAHEAD(3) SelectStatement() | SqlExpression()) ")"
  | BindVariable() { push(ExpressionNode.BIND_VARIABLE, token); }
  | LOOKAHEAD(2) SqlCastExpression()
  | LOOKAHEAD(IntervalExpression()) IntervalExpression()
  | LOOKAHEAD(FunctionReference() "(") FunctionCall()
  | ObjectReference() // Might be a call to a parameter-less function.
}

void SqlCaseExpression():
{int paramCount = 0;}
{
//    <K_CASE> <K_WHEN> SqlExpression() <K_THEN> SqlSimpleExpression()
//    ["ELSE" SqlSimpleExpression()]
//    "END"
     <K_CASE> ((SqlSimpleExpression() { paramCount = 1; } (<K_WHEN> SqlSimpleExpression() <K_THEN> SqlSimpleExpression() { paramCount+=2; })*)
            | (<K_WHEN> SqlExpression() <K_THEN> SqlSimpleExpression() { paramCount+=2; })*)
        [<K_ELSE> SqlSimpleExpression(){ paramCount++; }]
        <K_END>
        {pop(ExpressionNode.FUNCTION, "CASE", token.beginColumn, paramCount);}
}

void SqlCastExpression():
{}
{
    "CAST" "(" SqlExpression() "AS" BasicDataTypeDefinition() ")"
    {pop(ExpressionNode.FUNCTION, "CAST", token.beginColumn, 2);}
}

void TableColumn():
{}
{
    ObjectReference()
}

void ObjectName():
{}
{
    <S_IDENTIFIER>          {
        push(ExpressionNode.LITERAL, token);
    }
    | <S_QUOTED_IDENTIFIER> {
        push(ExpressionNode.LITERAL, token.image.subSequence(1, token.image.length() - 1), token.beginColumn);
    }
}

CharSequence TNSName():
{
    StringBuilder name = new StringBuilder();
}
{
    <S_IDENTIFIER>      {name.append(token.image);}
    ("." <S_IDENTIFIER> {name.append(".").append(token.image);} )*
    {return name.toString();}
}

void Relop():
{}
{
    "="
  | "!" "="
  | "#"
  | LOOKAHEAD(2) ">" "="
  | ">"
  | LOOKAHEAD(2) "<" ">"
  | LOOKAHEAD(2) "<" "="
  | "<"
}

void TableName():
{}
{
    // table
    ObjectName()
}

void ParameterList():
{}
{
    Parameter() ( "," Parameter() )*
}

void NumOrID():
{}
{
    <S_IDENTIFIER> | (["+" | "-"] <S_NUMBER>)
}

void Parameter():
{}
{
    <S_IDENTIFIER> [ ["IN"] ["OUT"] BasicDataTypeDefinition()
                             [(":=" | "DEFAULT" ) SqlExpression()] ]
}

void Arguments():
{}
{
    Argument() ("," Argument())*
}

void Argument():
{}
{
    [LOOKAHEAD(2) <S_IDENTIFIER> "=>"] SqlExpression()
}

void SelectStatement():
{}
{
    SelectWithoutOrder()
    [ OrderByClause() ]
}

void SelectWithoutOrder():
{ String operation; }
{
    SelectSet() ((("UNION"  { operation = "UNION"; }
                            ["ALL" { operation = "UNION ALL"; } ] )
     | "INTERSECT"  { operation = "INTERSECT";}
     | "MINUS" { operation = "MINUS";} ) SelectSet()
        { pop(ExpressionNode.SET_OPERATION,operation, token.beginColumn, 2);})*
}

void SelectSet():
{}
{
    Select() | "(" SubQuery() ")"
}

void Select():
{ String modifier = null; }
{
    "SELECT" [ "ALL" | "DISTINCT" | "UNIQUE" {modifier = token.image;}] SelectList() { popSelect(modifier); }
    FromClause() { popFrom(); }
    [ WhereClause() { popWhere(); } ]
}

void SelectList():
{}
{
    "*" | SelectItem() ("," SelectItem())*
}

void SelectItem():
{}
{
    (
        LOOKAHEAD(2) ObjectName()".*" // table.*
      | LOOKAHEAD(4) ObjectName()"." ObjectName() ".*" // schema.table.*
      | SqlSimpleExpression() // column name or expression
    )
    [ [ "AS" ] SelectItemAlias()]
}

void SelectItemAlias():
{}
{
    ObjectName()
    // Some keywords are acceptable as aliases:
  | "RETURNING" | "WHEN"
}

void AnalyticFunction():
{}
{
    FunctionCall() [ "OVER" "(" AnalyticClause() ")" ]
}

void AnalyticClause():
{}
{
    [ QueryPartitionClause() ] [ OrderByClause() [ WindowingClause() ] ]
}

void QueryPartitionClause():
{}
{
    "PARTITION" "BY" SqlExpression() ( "," SqlExpression() )*
}

void WindowingClause():
{}
{
    ("ROWS" | "RANGE")
    ( "CURRENT" "ROW"
    | SqlSimpleExpression() ID("PRECEDING") // perhaps UNBOUNDED PRECEDING
    | "BETWEEN" ( "CURRENT" "ROW"
                | SqlSimpleExpression() ID("PRECEDING|FOLLOWING") // perhaps UNBOUNDED FOLLOWING
                )
          "AND" ( "CURRENT" "ROW"
                | SqlSimpleExpression() ID("PRECEDING|FOLLOWING") // perhaps UNBOUNDED PRECEDING
                )
    )
}

void IntoClause():
{}
{
   "INTO" DataItem() ("," DataItem())*
}

void DataItem():
{}
{
    (<S_IDENTIFIER> ["." <S_IDENTIFIER>] | BindVariable())
    [ "(" SqlSimpleExpression() ")" ] // collection subscript
}

void FromClause():
{}
{
    "FROM" TableReference() ( "," TableReference())*
}
void TableReference():
{}
{
    "ONLY" "(" QueryTableExpression() ")"
  | QueryTableExpression()
}

void QueryTableExpression():
{}
{
    (TableName() // might also be a query name
     | TableCollectionExpression()
     | LOOKAHEAD(3) "(" SubQuery() ")"
     | "(" TableReference() ")"
     | BindVariable() // not valid Sql, but appears in StatsPack Sql text
    )
    ["PX_GRANULE" "(" <S_NUMBER> "," <S_IDENTIFIER> "," <S_IDENTIFIER> ")"]
    ["SAMPLE" [ID("BLOCK")] "(" <S_NUMBER> ")"]
    [ObjectName()] // alias
    (Join())*
}

void TableCollectionExpression():
{}
{
    "TABLE" "(" SqlSimpleExpression() ")" [ "(" "+" ")" ]
}

void Join():
{}
{
    JoinType() ID("JOIN") TableReference() ("ON" SqlExpression())
}

void JoinType():
{}
{
    "INNER"
  | ("LEFT" | "RIGHT" | "FULL") ID("OUTER")
}

void ColumnName():
{}
{
    <S_IDENTIFIER>
}

void WhereClause():
{}
{
    "WHERE" SqlExpression()
}

void OrderByClause():
{}
{
    "ORDER" "BY" OrderByExpression() ("," OrderByExpression())*
}

void OrderByExpression():
{}
{
    SqlSimpleExpression()
    ["ASC" | "DESC"]
    ["NULLS" ID("LAST")]
}

void ExistsClause():
{}
{
    ["NOT"] "EXISTS" "(" SubQuery() ")"
}

void IntervalExpression():
{}
{
    ID("INTERVAL") SqlSimpleExpression()
    ( LOOKAHEAD({"DAY".equalsIgnoreCase(getToken(1).image)})
      ID("DAY") ["(" <S_NUMBER> ")"] "TO" ID("SECOND") ["(" <S_NUMBER> ")"]
    | ID("YEAR") ["(" <S_NUMBER> ")"] "TO" ID("MONTH") ["(" <S_NUMBER> ")"]
    )
}

void FunctionCall():
{}
{
    FunctionReference()
//    (
//        LOOKAHEAD({"TRIM".equalsIgnoreCase(lastObjectReference)}) TrimArguments()
//      | LOOKAHEAD({"EXTRACT".equalsIgnoreCase(lastObjectReference)}) "(" DatetimeField() "FROM" SqlSimpleExpression() ")"
//      | [ "(" [["ALL" | "DISTINCT" | "UNIQUE"] (FunctionArgumentList() | "*")] ")" ]
//    )
    // "all/distinct/unique/*" are permitted only with aggregate functions,
    // but this parser allows their use with any function.
}

void FunctionReference():
{}
{
    ObjectReference()
}

void FunctionArgumentList():
{}
{
    FunctionArgument() ("," FunctionArgument())*
}

void FunctionArgument():
{}
{
    [LOOKAHEAD(2) <S_IDENTIFIER> "=>"] SqlExpression()
}

void TrimArguments():
{}
{
    "(" ( LOOKAHEAD({getToken(1).image.matches("(?i)LEADING|TRAILING|BOTH")})
            <S_IDENTIFIER> [SqlSimpleExpression()] "FROM" SqlSimpleExpression()
        | SqlSimpleExpression() ["FROM" SqlSimpleExpression()]
        )
    ")"
}

void DatetimeField():
{}
{
    <S_IDENTIFIER>
}

void ObjectReference():
{
    int paramCount = 1;
    String literal0 = null;
}
{
    ObjectName()     { literal0 = token.image; }
    [ "." ObjectName() { literal0 += "." + token.image; paramCount++; }
    [ "." ObjectName() { literal0 += "." + token.image; paramCount++; }
    ]]
    {if (paramCount == 2) {
        replace(ExpressionNode.LITERAL, literal0, token.beginColumn, paramCount);
    }}
}

void OuterJoinExpression():
{}
{
    TableColumn() "(" "+" ")"
}

void SubQuery():
{}
{
    SelectStatement()
}

/** Expect an <S_IDENTIFIER> with the given value. */
void ID(String id):
{}
{
    <S_IDENTIFIER>
    {
        if (!token.image.matches("(?i)" + id)) {
            throw new ParseException("Encountered " + token.image
                + " at line " + token.beginLine + ", column " + token.beginColumn + "."
                + "\nWas expecting: " + id);
        }
    }
}